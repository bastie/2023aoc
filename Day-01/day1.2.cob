IDENTIFICATION DIVISION.
PROGRAM-ID. Day1Silver.

ENVIRONMENT DIVISION.
  INPUT-OUTPUT SECTION.
    FILE-CONTROL.
      SELECT PUZZLE-FILE ASSIGN TO 'puzzle.in'
             ORGANIZATION IS LINE SEQUENTIAL
      .

DATA DIVISION.
FILE SECTION.
FD PUZZLE-FILE.
  01 PUZZLE-LINE.
    05 PUZZLE-CONTENT PIC X(90).

WORKING-STORAGE SECTION.
*> --- file data ---
  77 FILE-END PIC X VALUE 'Y'.
  01 FILE-END-FLAG PIC X VALUE LOW-VALUE.
    88 FILE-END-TRUE VALUE 'Y'.
*> --- work data ---
  77 POSITION-IN-LINE   PIC 9(2).
  01 PUZZLE-NUMBER.
    05 FIRST-NUMBER       PIC X.
      88 FIRST-IS-NUMBER VALUE 0 THRU 9.
    05 LAST-NUMBER        PIC X.
      88 LAST-IS-NUMBER VALUE 0 THRU 9.
  01 PUZZLE-NUMBER-NUMERIC REDEFINES PUZZLE-NUMBER PIC 99.
  77 PUZZLE-NUMBER-SUM PIC 99999.

*> =============================================== <*
   PROCEDURE DIVISION.
*> =============================================== <*
MAIN SECTION.
    OPEN INPUT PUZZLE-FILE

    PERFORM READ-LOOP UNTIL FILE-END-TRUE

    CLOSE PUZZLE-FILE

    DISPLAY "RESULT = " PUZZLE-NUMBER-SUM
    
    GOBACK
. *>END SECTION

*> ----------------------------------------------- <*
READ-LOOP SECTION.
    READ PUZZLE-FILE
    AT END MOVE FILE-END TO FILE-END-FLAG
    NOT AT END
        PERFORM NORMALIZE-LINE
        PERFORM HANDLE-LINE
. *>END SECTION

*> ----------------------------------------------- <*
NORMALIZE-LINE SECTION.
  *> in result of lines like "threeightwo" a simple
  *> replacement of "one" by "111" or by "1  " failed 
  INSPECT PUZZLE-LINE REPLACING ALL "one"   BY "o1e"
  INSPECT PUZZLE-LINE REPLACING ALL "two"   BY "t2o"
  INSPECT PUZZLE-LINE REPLACING ALL "three" BY "th3ee"
  INSPECT PUZZLE-LINE REPLACING ALL "four"  BY "f4ur"
  INSPECT PUZZLE-LINE REPLACING ALL "five"  BY "f5ve"
  INSPECT PUZZLE-LINE REPLACING ALL "six"   BY "s6x"
  INSPECT PUZZLE-LINE REPLACING ALL "seven" BY "se7en"
  INSPECT PUZZLE-LINE REPLACING ALL "eight" BY "ei8ht"
  INSPECT PUZZLE-LINE REPLACING ALL "nine"  BY "ni9e"
. *>END SECTION

*> ----------------------------------------------- <*
HANDLE-LINE SECTION.
    PERFORM VARYING POSITION-IN-LINE FROM 1 BY 1 UNTIL POSITION-IN-LINE > 90
      MOVE PUZZLE-LINE (POSITION-IN-LINE:1) TO FIRST-NUMBER
      IF FIRST-IS-NUMBER
        MOVE 91 TO POSITION-IN-LINE 
      END-IF
    END-PERFORM
 
    PERFORM VARYING POSITION-IN-LINE FROM 90 BY -1 UNTIL POSITION-IN-LINE < 1
      MOVE PUZZLE-LINE (POSITION-IN-LINE:1) TO LAST-NUMBER
      IF LAST-IS-NUMBER
        MOVE 1 TO POSITION-IN-LINE
      END-IF
    END-PERFORM

    ADD PUZZLE-NUMBER-NUMERIC TO PUZZLE-NUMBER-SUM

    MOVE ALL SPACES TO PUZZLE-LINE
    MOVE ALL SPACES TO PUZZLE-NUMBER
. *>END SECTION

*> === EOF ====================================== <*
